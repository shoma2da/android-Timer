apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.hatenablog.shoma2da.android.timer"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 109
        versionName "1.2.0"
    }
    signingConfigs {
        release {
            storeFile file("../keystore/shoma2da_key")
            storePassword "$System.env.KEYSTORE_PASSPHRASE"
            keyAlias "$System.env.ALIAS_NAME"
            keyPassword "$System.env.ALIAS_PASS"
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android.gms:play-services-ads:8.4.0'
    compile 'com.google.android.gms:play-services-analytics:8.4.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
//    compile 'com.parse.bolts:bolts-android:1.+'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.jakewharton.timber:timber:4.1.0'
}
buildscript {
    ext.kotlin_version = '1.0.0-beta-4584'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.deploygate:gradle:0.6'
    }
}
apply plugin: 'deploygate'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

deploygate {
    userName = "shoma2da"
    token = "$System.env.DEPLOYGATE_TOKEN"

    apks {
        release {
            sourceFile = file("./build/outputs/apk/app-release.apk")
            message = "build number is $System.env.TRAVIS_BUILD_NUMBER on $System.env.TRAVIS_BRANCH branch."
        }

        debug {
            sourceFile = file("./build/outputs/apk/app-debug.apk")
            message = "build number is $System.env.TRAVIS_BUILD_NUMBER on $System.env.TRAVIS_BRANCH branch."
        }
    }
}
